MACRO(GITTEST_AUX_SOURCES_ADD_PLATFORM lump plat)
  # for lump=LIB , plat=WIN
  # append GITTEST_LIB_HEADERS_WIN (or SOURCES_WIN)
  # to     GITTEST_LIB_HEADERS     (or SOURCES)
  IF (DEFINED GITTEST_${lump}_HEADERS_${plat})
    LIST(APPEND GITTEST_${lump}_HEADERS ${GITTEST_${lump}_HEADERS_${plat}})
  ENDIF ()
  IF (DEFINED GITTEST_${lump}_SOURCES_${plat})
    LIST(APPEND GITTEST_${lump}_SOURCES ${GITTEST_${lump}_SOURCES_${plat}})
  ENDIF ()
ENDMACRO ()

FUNCTION (GITTEST_AUX_SOURCES_ENSURE_HEADERS_SOURCES_DEFINED lump)
  IF ((NOT (DEFINED GITTEST_${lump}_HEADERS AND
           (DEFINED GITTEST_${lump}_SOURCES))))
    MESSAGE(FATAL_ERROR "GITTEST_[${lump}]_ HEADERS or SOURCES not defined?")
  ENDIF()
ENDFUNCTION ()

FUNCTION (GITTEST_AUX_SOURCES_SET_MAGIC_EMPTY varname)
  SET("${varname}" ";" PARENT_SCOPE)
ENDFUNCTION ()

FUNCTION (GITTEST_AUX_SOURCES_ENSURE_MAGIC_TARGET_LIST_HAVE)  # varargs
  # early exit on missing required (global) definition
  IF (NOT (DEFINED GITTEST_SOURCES_MAGIC_TARGET_LIST))
    MESSAGE(FATAL_ERROR "GITTEST_SOURCES_MAGIC_TARGET_LIST definition missing?")
  ENDIF ()
  # compare the contents
  FOREACH (TMPTARGET IN LISTS ARGN) # varargs ARGN
    IF (NOT ("${TMPTARGET}" IN_LIST GITTEST_SOURCES_MAGIC_TARGET_LIST))
      MESSAGE(FATAL_ERROR "target list mismatch (elt)")
    ENDIF ()
  ENDFOREACH ()
ENDFUNCTION ()
